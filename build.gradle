plugins {
  id 'java-library'
  id 'uk.gov.hmcts.java' version '0.12.67'
  id 'checkstyle'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.1.7'
  id 'org.owasp.dependencycheck' version '12.1.3'
  id 'com.github.ben-manes.versions' version '0.53.0'
  id 'org.sonarqube' version '7.0.1.6134'
  id 'io.freefair.lombok' version '9.0.0'
  id 'maven-publish'
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
    }
  }
  repositories {
    maven {
      name = "AzureArtifacts"
      url = uri("https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1")
      credentials {
        username = System.getenv("AZURE_DEVOPS_ARTIFACT_USERNAME")
        password = System.getenv("AZURE_DEVOPS_ARTIFACT_TOKEN")
      }
    }
  }
}

def buildNumber = System.getenv("RELEASE_VERSION")?: "DEV-SNAPSHOT"

group = 'com.github.hmcts'
version = buildNumber

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
}

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

// https://github.com/gradle/gradle/issues/16791
tasks.withType(JavaExec).configureEach {
  javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

tasks.withType(Test) {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

test {
  failFast = true
}

checkstyle {
  toolVersion = '12.1.1'
}

jacocoTestReport {
  executionData(test)
  reports {
    xml.required = true
    csv.required = false
    xml.outputLocation.set(layout.buildDirectory.file("reports/jacoco/test/jacocoTestReport.xml"))
  }
}

project.tasks['sonarqube'].dependsOn jacocoTestReport

sonarqube {
  properties {
    property "sonar.projectName", "PIP Data Models"
    property "sonar.projectKey", "pip-data-models"
    property "sonar.exclusions", sonarExclusions.join(', ')
  }
}

configurations {
  all {
    exclude group: 'commons-logging', module: 'commons-logging'
  }
}

// before committing a change, make sure task still works
dependencyUpdates {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf { selection -> // <---- notice how the closure argument is named
    return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  suppressionFile = 'config/owasp/suppressions.xml'
  skipTestGroups  = false
}

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url = 'https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1'
  }
}

ext {
  log4JVersion = '2.25.2'
  spring = '3.5.7'
}

dependencies {
  implementation "org.springframework.boot:spring-boot-starter-json:${spring}"
  implementation "org.springframework.boot:spring-boot-starter-data-jpa:${spring}"
  implementation "org.springframework.boot:spring-boot-starter-oauth2-client:${spring}"
  implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.14"

  implementation "org.apache.logging.log4j:log4j-api:${log4JVersion}"
  implementation "org.apache.logging.log4j:log4j-to-slf4j:${log4JVersion}"
  implementation 'org.eclipse.angus:angus-activation:2.0.3'

  implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.20.1"
  implementation "com.opencsv:opencsv:5.12.0"

  testRuntimeOnly "org.junit.platform:junit-platform-launcher"

  testImplementation "org.springframework.boot:spring-boot-starter-test:${spring}"
  testImplementation "com.github.hmcts:fortify-client:1.4.10:all"
  implementation 'org.apache.commons:commons-lang3:3.19.0'
}

tasks.register('fortifyScan', JavaExec) {
  mainClass = "uk.gov.hmcts.fortifyclient.FortifyClientMainApp"
  classpath += sourceSets.test.runtimeClasspath
  jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}
